# Maintainer: ImperatorS79 <fevrier.simon@gmail.com>

_realname=ffnvcodec-headers
pkgbase=mingw-w64-jellyfin-${_realname}
pkgname="${MINGW_PACKAGE_PREFIX}-jellyfin-${_realname}"
pkgver=12.0.16.1
pkgrel=1
pkgdesc='FFmpeg version of headers required to interface with Nvidias codec APIs (mingw-w64)'
arch=('any')
mingw_arch=('mingw32' 'mingw64' 'ucrt64' 'clang64' 'clang32' 'clangarm64')
url=https://github.com/FFmpeg/nv-codec-headers.git
license=('spdx:MIT')
makedepends=("git")
_tag=43d91706e097565f57b311e567f0219838bcc2f6
source=(git+https://github.com/FFmpeg/nv-codec-headers.git#tag=${_tag})
sha256sums=('SKIP')

export MINGW_TOOLCHAIN_PREFIX="${MINGW_PREFIX}"
export FF_MINGW_PREFIX="${MINGW_TOOLCHAIN_PREFIX}/ffbuild"

pkgver() {
  cd nv-codec-headers

  git describe --tags | sed 's/^n//'
}

build() {
  make PREFIX=${FF_MINGW_PREFIX} -C nv-codec-headers
  sed -n '4,25p' nv-codec-headers/include/ffnvcodec/nvEncodeAPI.h > LICENSE # Extract license
  sed -i '1,22s/^.\{,3\}//' LICENSE # Delete C comments
}

package() {
  make PREFIX=${FF_MINGW_PREFIX} DESTDIR="${pkgdir}" -C nv-codec-headers install
  install -Dm 644 LICENSE -t "${pkgdir}${FF_MINGW_PREFIX}"/share/licenses/ffnvcodec-headers/

  local PREFIX_WIN=$(cygpath -wm ${FF_MINGW_PREFIX})
  for pcfile in "${pkgdir}${FF_MINGW_PREFIX}"/lib/pkgconfig/*.pc; do
    sed -s "s|${PREFIX_WIN}|${FF_MINGW_PREFIX}|g" -i "${pcfile}"
  done
}
